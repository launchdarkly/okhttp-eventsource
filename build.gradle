
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id "java"
    id "java-library"
    id "checkstyle"
    id "jacoco"
    id "signing"
    id "maven-publish"
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id "io.codearte.nexus-staging" version "0.21.2"
    id "org.ajoberstar.git-publish" version "2.1.3"
    id "idea"
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
}

allprojects {
    group = 'com.launchdarkly'
    version = "${version}"
    archivesBaseName = 'okhttp-eventsource'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

ext.versions = [
    "okhttp": "4.5.0",
    "slf4j": "1.7.22"
]

dependencies {
    api "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    api "org.slf4j:slf4j-api:${versions.slf4j}"
    testImplementation "ch.qos.logback:logback-classic:1.1.9"
    testImplementation "org.mockito:mockito-core:1.10.19"
    testImplementation "org.eclipse.jetty:jetty-server:9.4.27.v20200227"
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-all:1.3" 
}

checkstyle {
    configFile file("${project.rootDir}/checkstyle.xml")
}

jar {
    manifest {
        attributes("Implementation-Version": project.version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    // Force the Javadoc build to fail if there are any Javadoc warnings. See: https://discuss.gradle.org/t/javadoc-fail-on-warning/18141/3
    // The '-quiet' as second argument is actually a hack,
    // since the one parameter addStringOption doesn't seem to
    // work, we extra add '-quiet', which is added anyway by
    // gradle. See https://github.com/gradle/gradle/issues/2354
    // See JDK-8200363 (https://bugs.openjdk.java.net/browse/JDK-8200363)
    // for information about the -Xwerror option.
    options.addStringOption('Xwerror', '-quiet')
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

jacocoTestReport { // code coverage report
    reports {
        xml.enabled
        csv.enabled true
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    // See notes in CONTRIBUTING.md on code coverage. Unfortunately we can't configure line-by-line code
    // coverage overrides within the source code itself, because Jacoco operates on bytecode.
    violationRules { rules ->
        def knownMissedLinesForMethods = [
            // The key for each of these items is the complete method signature minus the "com.launchdarkly.eventsource." prefix.
            "EventSource.connect()": 12,
            "EventSource.Builder.createInitialClientBuilder()": 1,
            "EventSource.Builder.defaultTrustManager()": 2,
            "SLF4JLogger.error(java.lang.String)": 2,
            "ModernTLSSocketFactory.createSocket(java.lang.String, int)": 1,
            "ModernTLSSocketFactory.createSocket(java.lang.String, int, java.net.InetAddress, int)": 1,
            "ModernTLSSocketFactory.createSocket(java.net.InetAddress, int)": 1,
            "ModernTLSSocketFactory.createSocket(java.net.InetAddress, int, java.net.InetAddress, int)": 1,
            "ModernTLSSocketFactory.createSocket(java.net.Socket, java.lang.String, int, boolean)": 1,
            "ModernTLSSocketFactory.getDefaultCipherSuites()": 1,
            "ModernTLSSocketFactory.getSupportedCipherSuites()": 1
        ]
        
        knownMissedLinesForMethods.each { partialSignature, maxMissedLines ->
            if (maxMissedLines > 0) {  // < 0 means skip entire method
                rules.rule {
                    element = "METHOD"
                    includes = [ "com.launchdarkly.eventsource." + partialSignature ]
                    limit {
                        counter = "LINE"
                        value = "MISSEDCOUNT"
                        maximum = maxMissedLines
                    }
                }
            }
        }
        
        // General rule that we should expect 100% test coverage; exclude any methods that have overrides above
        rule {
            element = "METHOD"
            limit {
                counter = "LINE"
                value = "MISSEDCOUNT"
                maximum = 0
            }
            excludes = knownMissedLinesForMethods.collect { partialSignature, maxMissedLines ->
                "com.launchdarkly.eventsource." + partialSignature
            }
        }
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

nexusStaging {
    packageGroup = "com.launchdarkly"
    numberOfRetries = 40 // we've seen extremely long delays in closing repositories
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.launchdarkly'
            artifactId = 'okhttp-eventsource'

            artifact sourcesJar
            artifact javadocJar
            
            pom {
                name = 'okhttp-eventsource'
                description = 'EventSource Implementation built on OkHttp'
                url = 'https://github.com/launchdarkly/okhttp-eventsource'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'LaunchDarkly'
                        email = 'team@launchdarkly.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/launchdarkly/okhttp-eventsource.git'
                    developerConnection = 'scm:git:ssh:git@github.com:launchdarkly/okhttp-eventsource.git'
                    url = 'https://github.com/launchdarkly/okhttp-eventsource'
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

nexusPublishing {
    clientTimeout = java.time.Duration.ofMinutes(2) // we've seen extremely long delays in creating repositories
    repositories {
        sonatype {
            username = ossrhUsername
            password = ossrhPassword            
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

gitPublish {
    repoUri = 'git@github.com:launchdarkly/okhttp-eventsource.git'
    branch = 'gh-pages'
    contents {
        from javadoc
    }
    commitMessage = 'publishing javadocs'
}
